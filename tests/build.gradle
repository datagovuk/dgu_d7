apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'


buildscript {
    apply from: 'https://github.com/samueltbrown/gradle-cucumber-plugin/raw/master/repo/gradle-cucumber-plugin/gradle-cucumber-plugin/0.2/cucumberinit.gradle'
} 

def version = [
        'groovy'    : '2.0.0-beta-2',
        'junit'     : '4.10',
        'geb'       : '0.7.2',  //geb-junit4 is only at 0.7.2 so we can't move to 0.9.0
        'selenium'  : '2.32.0',
        'cucumber'  : '1.0.11'
]


configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

repositories {
    mavenCentral()
}

def drivers = ["firefox", "chrome"]

dependencies {
    // Groovy library for groovy building!
    groovy "org.codehaus.groovy:groovy-all:$version.groovy"

    //Extend the cucumber runtime with dependencies which included the compiled test sources
    cucumberRuntime "info.cukes:cucumber-groovy:$version.cucumber",
            "org.codehaus.groovy:groovy-all:$version.groovy",
            files("${jar.archivePath}"),
            files(sourceSets.test.output),
            files("src/test/resources")


    //Include some dependencies for the tests (junit, geb, cucumber and cucumber-junit)
    testCompile "junit:junit:$version.junit"
    testCompile "org.codehaus.geb:geb-junit4:$version.geb"
    testCompile "info.cukes:cucumber-groovy:$version.cucumber"
    testCompile "info.cukes:cucumber-junit:$version.cucumber"

    //Include a dependency for each web driver we are using.
    drivers.each { driver ->
        testCompile "org.seleniumhq.selenium:selenium-$driver-driver:$version.selenium"
    }
}

//This configures the external cucumber gradle plugin but it seems not to work.
cucumber {
    formats = ['pretty','json:build/cucumber.json','junit:build/cucumber.xml']
    glueDirs = ['src/test/groovy' ]
    featureDirs = ['src/test/resources/features']
    monochrome = false
    strict = false
    dryRun = false
}

//define our own javaexec task to run cucumber tests, using the cucumberRuntime.
task cuke(){
    dependsOn assemble, compileTestGroovy
    doLast {
        javaexec {
            main = "cucumber.cli.Main"
            classpath = configurations.cucumberRuntime
            args = ['-f', 'pretty', '--glue', 'src/test/groovy', 'src/test/resources/features']

        }
    }
}
